buildscript {
	ext {
		springBootVersion = '1.5.1.RELEASE'
	}
    repositories {
//		mavenCentral()
        maven {
            url "${nexusUrl}/repository/maven-public"
        }
// Spring Boot 1.5 required using a milestone version of spring cloud dependencies bom to get zuul working
        maven { url "https://repo.spring.io/milestone" }
    }
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'se.transmode.gradle:gradle-docker:1.2'
	}
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven'
apply plugin: 'docker'


jar {
	baseName = 'rsw-gateway'
	version = '0.0.1'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

docker {
//    useApi true
//    hostUrl 'http://'
//    apiUsername 'user'
//    apiPassword 'password'
//    apiEmail 'mail@abc.com'
}

task buildDocker(type: Docker, dependsOn: build) {
    push = true
    // miniserver.local : Nexus hosted docker registry
    tag = "10.0.1.9:8082/rsw-gateway"
    tagVersion = "v1"
    dockerfile = file('src/main/docker/Dockerfile')
    applicationName = jar.baseName
    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}

repositories {
//	mavenCentral()
    maven {
        url "${nexusUrl}/repository/maven-public"
    }
    maven { url "https://repo.spring.io/milestone" }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "${nexusUrl}/repository/maven-releases/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }
            snapshotRepository(url: "${nexusUrl}/repository/maven-snapshots") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }
        }
    }
}

dependencies {
	compile('org.springframework.cloud:spring-cloud-starter-zuul')
	compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.cloud:spring-cloud-starter-oauth2')
    compile('org.springframework.cloud:spring-cloud-starter-security')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-data-redis')
    compile('org.springframework.session:spring-session')
    compile('com.google.guava:guava:19.0')
    compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
    compile('io.springfox:springfox-swagger2:2.6.0')
    compile('io.springfox:springfox-swagger-ui:2.6.0')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
// Spring Boot 1.5.1 requires using a milestone version of spring cloud dependencies bom to avoid zuul problem ...
//   ... ServletRegistrationBean NoClassDefFound due to bean moving to a different package
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:Dalston.M1"
	}
}
